# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=breeze
pkgname=(breeze breeze-kde4)
pkgver=5.12.3
pkgrel=1.1
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
makedepends=(extra-cmake-modules frameworkintegration kdelibs automoc4 kdecoration kcmutils plasma-framework python)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig}
D11069.diff
D11175.diff
D11198.diff)
sha256sums=('1cbace92ba7548d90c6a74683bceec67a2b86ba7974238c85e3c94fc58577656'
            'SKIP'
            '2cf4623a2159f1711556105a6de36f285d2c9802c6ec15112fce00a09531e403'
            '78d19656fc9d65ce1ddf11b55e772977a1bd216908edeedffa178c55259f0bda'
            'c591b9206620e750d3733289fa30a1fae105fc6432358d861ebb97c4d84a854a')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build{,-kde4}

	cd $pkgname-$pkgver
	
	msg "D11069 patch" # See: https://phabricator.kde.org/D11069
	  patch -p1 -i ../D11069.diff
	msg "D11175 patch" # See: https://phabricator.kde.org/D11175 
	  patch -p1 -i ../D11175.diff
	msg "D11198 patch" # See: https://phabricator.kde.org/D11198
    patch -p1 -i ../D11198.diff
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make

  cd ../build-kde4
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_KDE4=ON \
    -DBUILD_TESTING=OFF
  make
}

package_breeze() {
  depends=(frameworkintegration kdecoration breeze-icons kwayland hicolor-icon-theme)
  pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
  optdepends=('breeze-kde4: Breeze widget style for KDE4 applications'
		'breeze-gtk: Breeze widget style for GTK applications'
		'kcmutils: for breeze-settings')
  groups=(plasma)

  cd build
  make DESTDIR="$pkgdir" install
}

package_breeze-kde4() {
  pkgdesc='Breeze widget style for KDE4 applications'
  depends=(kdelibs)

  cd build-kde4
  make DESTDIR="$pkgdir" install

  # needed for pure Qt4 apps
  install -d "$pkgdir"/usr/lib/qt4/plugins/styles
  ln -s /usr/lib/kde4/plugins/styles/breeze.so "$pkgdir"/usr/lib/qt4/plugins/styles
}
